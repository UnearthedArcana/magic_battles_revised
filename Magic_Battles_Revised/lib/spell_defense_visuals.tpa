//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								BETTER VISUALS
//__________________________________________________________________________________
//__________________________________________________________________________________


DEFINE_ACTION_FUNCTION better_visuals BEGIN

/*
***** look out for items - e.g. some IR stuff casts fire shield, some stuff applies nondetection, etc.

1) zero out hardcoded animations - replace:
 - spturni2.bam
 - spmagglo.bam
 - spshield.bam.
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/spmagglo.bam~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spmagglo.vvc~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spturni2.bam~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spturni2.vvc~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spshield.bam~ ~override~

/*
2) remove ioun stone animnations
 - copy helm18.itm
 - read name strref
 - read string from the strref
 - find any items with the same name strref or whose name matches the string
 - delete any 215 effects with timing 2
*/
ACTION_IF (FILE_EXISTS_IN_GAME ~helm18.itm~) BEGIN
  COPY_EXISTING ~helm18.itm~ ~override~
    READ_LONG 0x08 ioun_strref
    GET_STRREF %ioun_strref% ioun_string
  BUT_ONLY
  COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
    READ_LONG 0x08 this_strref
    GET_STRREF %this_strref% this_string
    PATCH_IF (%this_strref% = %ioun_strref%) OR (~%this_string%~ STRING_EQUAL_CASE ~%ioun_string%~) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 215 match_timing = 2 END
    END
  BUT_ONLY
END 

/* 
3) add mybulbgr vvc and bam (renamed) to PfNW
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/mybulbgr.bam~ ~override/dkbulbgr.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/mybulbgr.vvc~ ~override/dkbulbgr.vvc~
  WRITE_ASCII 0x08 ~dkbulbgr~ #8

COPY_EXISTING ~spwi511.spl~ ~override~
  READ_LONG 0x08 spl_name
  LPF DELETE_EFFECT INT_VAR match_opcode = 215 END // maybe not? this seems to be a one-time visual
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 120 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkbulbgr~ END
IF_EXISTS BUT_ONLY

/* 
// find all spells w/ same namne?
// ...but that will find ones that cast the original via 146... don't want to patch them
COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  READ_LONG 0x08 this_name
  PATCH_IF (%this_name% = %spl_name%) BEGIN
  END
BUT_ONLY
*/


/*
4) add spcomett vvc and bam (renamed) to PfMW
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/spcomett.bam~ ~override/dkcomett.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/spcomett.vvc~ ~override/dkcomett.vvc~
  WRITE_ASCII 0x08 ~dkcomett~ #8

COPY_EXISTING ~spwi611.spl~ ~override~
  READ_LONG 0x08 spl_name
  LPF DELETE_EFFECT INT_VAR match_opcode = 215 END // maybe not? this seems to be a one-time visual
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkcomett~ END
IF_EXISTS BUT_ONLY

/*
5) add modified dvmantle to prismatic mantle (unless SDRT)
*/
ACTION_IF !(MOD_IS_INSTALLED ~d5_random_tweaks.tp2~ ~2708~) BEGIN
  COPY ~%MOD_FOLDER%/data/bam-vvc/dvmantle.bam~ ~override~
  COPY ~%MOD_FOLDER%/data/bam-vvc/dvmantle.vvc~ ~override~
END

/* no need
  COPY_EXISTING ~spwi708.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 215 END // maybe not? this seems to be a one-time visual
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 120 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkcomett~ END
  BUT_ONLY
END
*/

/*
6) add myimpman vvc and bam (renamed) to moment of prescience and delete existing 215 effect
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/myimpman.bam~ ~override/dkimpman.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/myimpman.vvc~ ~override/dkimpman.vvc~
  WRITE_ASCII 0x08 ~dkimpman~ #8

COPY_EXISTING ~spwi808.spl~ ~override~
//  LPF DELETE_EFFECT INT_VAR match_opcode = 215 END // maybe not? this seems to be a one-time visual
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 0 STR_VAR resource = ~dkimpman~ END
IF_EXISTS BUT_ONLY

/*
7) add spabsimm vvc and bam (renamed) to absolute immunity
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/spabsimm.bam~ ~override/dkabsimm.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/spabsimm.vvc~ ~override/dkabsimm.vvc~
  WRITE_ASCII 0x08 ~dkabsimm~ #8

COPY_EXISTING ~spwi907.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkabsimm~ END
IF_EXISTS BUT_ONLY

/*
8) add ion1red & ion2red vvc and bam (renamed) to Pro Missiles
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1red.bam~ ~override/dkun1red.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1red.vvc~ ~override/dkun1red.vvc~
  WRITE_ASCII 0x08 ~dkun1red~ #8
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2red.bam~ ~override/dkun2red.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2red.vvc~ ~override/dkun2red.vvc~
  WRITE_ASCII 0x08 ~dkun2red~ #8

COPY_EXISTING ~spwi311.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 83 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun1red~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 83 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun2red~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 156 END
IF_EXISTS BUT_ONLY

/*
9) add ion1blck & ion2blck vvc and bam (renamed) to Nondetection
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1blck.bam~ ~override/dkun1blk.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1blck.vvc~ ~override/dkun1blk.vvc~
  WRITE_ASCII 0x08 ~dkun1blk~ #8
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2blck.bam~ ~override/dkun2blk.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2blck.vvc~ ~override/dkun2blk.vvc~
  WRITE_ASCII 0x08 ~dkun2blk~ #8

COPY_EXISTING ~spwi310.spl~ ~override~
			  ~spwi592.spl~ ~override~
			  ~dwsw310.spl~ ~override~
			  ~dwsw592.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun1blk~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun2blk~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 156 END
IF_EXISTS BUT_ONLY

/*
10) add swirlw vvc and bam (renamed) to MSD (white)
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlw.bam~ ~override/dkswrlw.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlw.vvc~ ~override/dkswrlw.vvc~
  WRITE_ASCII 0x08 ~dkswrlw~ #8

COPY_EXISTING ~spwi318.spl~ ~override~
			  ~dwsw318.spl~ ~override~
			  ~d5radfl.spl~ ~override~
			  ~d5f2318.spl~ ~override~
			  ~d5p2318.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 201 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlw~ END
IF_EXISTS BUT_ONLY

/*
11) add swirlb vvc and bam (renamed) to SD (blue)
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlb.bam~ ~override/dkswrlb.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlb.vvc~ ~override/dkswrlb.vvc~
  WRITE_ASCII 0x08 ~dkswrlb~ #8

COPY_EXISTING ~spwi522.spl~ ~override~
			  ~spwi618.spl~ ~override~
			  ~dwsw522.spl~ ~override~
			  ~d5f2522.spl~ ~override~
			  ~d5p2522.spl~ ~override~
			  ~spin710.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 201 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlb~ END
IF_EXISTS BUT_ONLY

/*
12) add swirly vvc and bam (renamed) to GSD (yellow)
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/swirly.bam~ ~override/dkswrly.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/swirly.vvc~ ~override/dkswrly.vvc~
  WRITE_ASCII 0x08 ~dkswrly~ #8

COPY_EXISTING ~spwi701.spl~ ~override~
			  ~dwsw701.spl~ ~override~
			  ~dw#mlstn.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 201 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrly~ END
IF_EXISTS BUT_ONLY

/*
13) add swirlp vvc and bam (renamed) to Spell Trap (purple)
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlp.bam~ ~override/dkswrlp.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlp.vvc~ ~override/dkswrlp.vvc~
  WRITE_ASCII 0x08 ~dkswrlp~ #8

COPY_EXISTING ~spwi902.spl~ ~override~
			  ~dwsw902.spl~ ~override~
			  ~staf11.spl~ ~override~
			  ~staf11b.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 201 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlp~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 102 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlp~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 259 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlp~ END
IF_EXISTS BUT_ONLY

/*
14) add swirlt vvc and bam (renamed) to Shield of the Archons (teal?)
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlt.bam~ ~override/dkswrlt.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/swirlt.vvc~ ~override/dkswrlt.vvc~
  WRITE_ASCII 0x08 ~dkswrlt~ #8

COPY_EXISTING ~sppr701.spl~ ~override~
			  ~dwsp701.spl~ ~override~
			  ~qdfv_701.spl~ ~override~
			  ~dgarchon.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 201 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkswrlt~ END
IF_EXISTS BUT_ONLY

/*
15) add discblue vvc aand bam (renamed) to k1#scre
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/discblue.bam~ ~override/dkdscblu.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/discblue.vvc~ ~override/dkdscblu.vvc~
  WRITE_ASCII 0x08 ~dkdscblu~ #8

COPY_EXISTING ~k1#scre.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkdscblu~ END

/*
16) overwrite minorglb vvc and bam in MGOI
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/minorglb.bam~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/minorglb.vvc~ ~override~

/*
17) add myglobep vvc and bam (renamed) in GOI
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/myglobep.bam~ ~override/dkglobep.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/myglobep.vvc~ ~override/dkglobep.vvc~
  WRITE_ASCII 0x08 ~dkglobep~ #8

COPY_EXISTING ~spwi602.spl~ ~override~
			  ~dwsw602.spl~ ~override~
			  ~staf12b.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 155 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkglobep~ END
IF_EXISTS BUT_ONLY

/*
18) add spfirest/spfiresr vvc and spfiresr bam (renamed) to fire shield red
*/
/*
// don't rename, just overwrite
COPY ~%MOD_FOLDER%/data/bam-vvc/spfiresr.bam~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spfiresr.vvc~ ~override~
  WRITE_ASCII 0x08 ~spfiresr~ #8
COPY ~%MOD_FOLDER%/data/bam-vvc/spfirest.vvc~ ~override~
  WRITE_ASCII 0x08 ~spfiresr~ #8
*/

/* don't need to copy
COPY_EXISTING ~spwi418.spl~ ~override~
			  ~dwsw418.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 155 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR resource = ~dkglobep~ END
*/

/*
19) replace mestsh.vvc with spfiresa/spfiresb vvc and spfiresb bam (renamed) in spwi526, dwsw526, spwi403, dwsw403, dw#w526a
COPY ~%MOD_FOLDER%/data/bam-vvc/spfiresb.bam~ ~override~
COPY ~%MOD_FOLDER%/data/bam-vvc/spfiresb.vvc~ ~override~
  WRITE_ASCII 0x08 ~spfiresb~ #8
COPY ~%MOD_FOLDER%/data/bam-vvc/spfiresa.vvc~ ~override~
  WRITE_ASCII 0x08 ~spfiresb~ #8

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_MESTILS_ACID_SHEATH[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_MESTILS_ACID_SHEATH~ RET spell_res END
  OUTER_SNPRINT (0 - 3) suffix_num spell_name 
  ACTION_IF !(FILE_EXISTS_IN_GAME ~dw#w%suffix_num%a.spl~) BEGIN
    COPY_EXISTING ~%spell_res%.spl~ ~override~
  				  ~dwsw%suffix_num%.spl~ ~override~
      LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 215 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR match_resource = ~mestsh~ resource = ~spfiresa~ END
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR match_resource = ~mestsh~ resource = ~spfiresb~ END
    IF_EXISTS BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~dw#w%suffix_num%a.spl~) BEGIN
    COPY_EXISTING ~dw#w%suffix_num%a.spl~ ~override~
      LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 215 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR match_resource = ~mestsh~ resource = ~spfiresa~ END
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR match_resource = ~mestsh~ resource = ~spfiresb~ END
    IF_EXISTS BUT_ONLY
  END
END

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_FIRE_SHIELD_BLUE[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_FIRE_SHIELD_BLUE~ RET spell_res END
  OUTER_SNPRINT (0 - 3) suffix_num spell_name 
  ACTION_IF !(FILE_EXISTS_IN_GAME ~dw#w%suffix_num%a.spl~) BEGIN
    COPY_EXISTING ~%spell_res%.spl~ ~override~
  				  ~dwsw%suffix_num%.spl~ ~override~
      LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 215 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR match_resource = ~mestsh~ resource = ~spfiresa~ END
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR match_resource = ~mestsh~ resource = ~spfiresb~ END
    IF_EXISTS BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~dw#w%suffix_num%a.spl~) BEGIN
    COPY_EXISTING ~dw#w%suffix_num%a.spl~ ~override~
      LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 215 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 2 STR_VAR match_resource = ~mestsh~ resource = ~spfiresa~ END
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR match_resource = ~mestsh~ resource = ~spfiresb~ END
    IF_EXISTS BUT_ONLY
  END
END
*/

/*
20) replace spturni2 with mybluorb vvc and bam (renamed) in spwi519, dwsw519, dw#w519a
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/mybluorb.bam~ ~override/dkbluorb.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/mybluorb.vvc~ ~override/dkbluorb.vvc~
  WRITE_ASCII 0x08 ~dkbluorb~ #8

ACTION_IF !(FILE_EXISTS_IN_GAME ~dw#w519a.spl~) BEGIN
  COPY_EXISTING ~spwi519.spl~ ~override~
			    ~dwsw519.spl~ ~override~
			    ~zpwi519.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 0 STR_VAR resource = ~dkbluorb~ END
  IF_EXISTS BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~dw#w519a.spl~) BEGIN
  COPY_EXISTING ~dw#w519a.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 206 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 0 STR_VAR resource = ~dkbluorb~ END
  IF_EXISTS BUT_ONLY
  COPY_EXISTING ~zpwi519.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 0 STR_VAR resource = ~dkbluorb~ END
  IF_EXISTS BUT_ONLY
END
/*
21) add mystorms vvc and bam (renamed) in [sppr322], spcl721, dwsc721
  [which is to say, CLERIC_STORM_SHIELD]
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/mystorms.bam~ ~override/dkstorms.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/mystorms.vvc~ ~override/dkstorms.vvc~
  WRITE_ASCII 0x08 ~dkstorms~ #8

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_STORM_SHIELD[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_STORM_SHIELD~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
  				~spcl721.spl~ ~override~
  				~dwsc721.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkstorms~ END
  IF_EXISTS BUT_ONLY
END

/*
22) add ion1gold/ion2gold vvc and ion1gold bam (renamed) to mind blank
*/
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1gold.bam~ ~override/dkun1gld.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion1gold.vvc~ ~override/dkun1gld.vvc~
  WRITE_ASCII 0x08 ~dkun1gld~ #8
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2gold.bam~ ~override/dkun2gld.bam~
COPY ~%MOD_FOLDER%/data/bam-vvc/ion2gold.vvc~ ~override/dkun2gld.vvc~
  WRITE_ASCII 0x08 ~dkun2gld~ #8

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_MIND_BLANK[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_MIND_BLANK~ RET spell_res END
  OUTER_SNPRINT (0 - 3) suffix_num spell_name
  COPY_EXISTING ~%spell_res%.spl~ ~override~
  				~spwi593.spl~ ~override~
  				~dwsw%suffix_num%.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun1gld~ END
    LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 215 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = ~dkun2gld~ END
  IF_EXISTS BUT_ONLY
END

END	//	end function
